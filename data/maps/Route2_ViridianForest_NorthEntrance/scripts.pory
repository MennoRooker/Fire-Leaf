mapscripts Route2_ViridianForest_NorthEntrance_MapScripts {
    MAP_SCRIPT_ON_LOAD: Route2_ViridianForest_NorthEntrance_OnLoad
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_SPOKE_TO_BUGSY, 0: Route2_ViridianForest_NorthEntrance_EventScript_Bugsy
    ]
}

script Route2_ViridianForest_NorthEntrance_OnLoad {
    setvar(VAR_REACHED_VIRIDIAN_FOREST_NORTH_GATE, 1)
    setflag(FLAG_ROUTE22_POLICEMAN_GONE)
}

script Route2_ViridianForest_NorthEntrance_EventScript_Bugsy {
    lockall
    playse(SE_PIN)
    applymovement(LOCALID_BUGSY_NORTH_GATE, Common_Movement_ExclamationMark)
    waitmovement(0)
    applymovement(LOCALID_BUGSY_NORTH_GATE, Common_Movement_Delay48)
    waitmovement(0)
    applymovement(LOCALID_BUGSY_NORTH_GATE, Route2_ViridianForest_NorthEntrance_Movement_BugsyApproach)
    waitmovement(0)
    msgbox(Route2_ViridianForest_NorthEntrance_Text_BugsyGreeting, MSGBOX_DEFAULT)
    closemessage
    setvar(VAR_SPOKE_TO_BUGSY, 1)
    applymovement(OBJ_EVENT_ID_PLAYER, Route2_ViridianForest_NorthEntrance_PlayerLetsBugsyPass)
    applymovement(LOCALID_BUGSY_NORTH_GATE, Route2_ViridianForest_NorthEntrance_Movement_BugsyLeaves)
    waitmovement(0)
    removeobject(LOCALID_BUGSY_NORTH_GATE)
    applymovement(OBJ_EVENT_ID_PLAYER, Route2_ViridianForest_NorthEntrance_PlayerLeavesAfterBugsy)
    waitmovement(0)
    setflag(FLAG_HIDE_BUGSY_NORTH_GATE)
    clearflag(FLAG_HIDE_BUGSY_VIRIDIAN_FOREST)
    setvar(VAR_BUGSY_VIRIDIAN_FOREST_SCENE, 1)
    warp(MAP_VIRIDIAN_FOREST, 5, 10)
    waitstate
    releaseall
}

movement Route2_ViridianForest_NorthEntrance_Movement_BugsyApproach {
    walk_down
    walk_down
}

movement Route2_ViridianForest_NorthEntrance_PlayerLetsBugsyPass {
    walk_left
    face_right
}

movement Route2_ViridianForest_NorthEntrance_Movement_BugsyLeaves {
    walk_down
    walk_down
}

movement Route2_ViridianForest_NorthEntrance_PlayerLeavesAfterBugsy {
	walk_right
    walk_down
}


raw`
Route2_ViridianForest_NorthEntrance_EventScript_Youngster::
	msgbox Route2_ViridianForest_NorthEntrance_Text_ManyMonsOnlyInForests, MSGBOX_NPC
	end

Route2_ViridianForest_NorthEntrance_EventScript_OldMan::
	msgbox Route2_ViridianForest_NorthEntrance_Text_CanCutSkinnyTrees, MSGBOX_NPC
	end

Route2_ViridianForest_NorthEntrance_EventScript_CooltrainerF::
	msgbox Route2_ViridianForest_NorthEntrance_Text_CanCancelEvolution, MSGBOX_NPC
	end

`